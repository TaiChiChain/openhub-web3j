apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: "de.marcphilipp.nexus-publish"

task javadocsJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task testsJar(type: Jar) {
    archiveClassifier = 'tests'
    from sourceSets.test.output
}

javadoc { options.encoding = 'UTF-8' }

artifacts { archives sourcesJar, javadocsJar, testsJar }

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocsJar
            artifact testsJar

            pom {
                name = project.name
                description.set(project.provider({ project.description }))
                url = 'https://github.com/axiomesh/axiom-web3j'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                scm {
                    def slug = rootProject.name
                    if (project.hasProperty('rootProjectOverride')) {
                        slug = project.rootProjectOverride
                    }

                    url = "https://github.com/axiomesh/$slug"
                    connection = "scm:https://github.com/axiomesh/${slug}.git"
                    developerConnection = "scm:https://github.com/axiomesh/${slug}.git"
                }
            }
        }
    }

    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri("https://maven.pkg.github.com/axiomesh/axiom-web3j")

            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME_GITHUB")
                password = project.findProperty("gpr.token") ?: System.getenv("TOKEN_GITHUB")
            }
        }
    }
}

//nexusPublishing {
//    repositories {
//        sonatype {
//            username = System.getenv('OSSRH_USERNAME')
//            password = System.getenv('OSSRH_PASSWORD')
//        }
//    }
//}
//
//
//nexusStaging {
//    username System.getenv('OSSRH_USERNAME')
//    password System.getenv('OSSRH_PASSWORD')
//    packageGroup = rootProject.group
//}

//signing {
//    sign publishing.publications.maven
//    def signingKey = new File("$rootDir/web3j.asc")
//    if (signingKey.exists()) {
//        useInMemoryPgpKeys(signingKey.getText('UTF-8'), System.getenv('GPG_PASSPHRASE'))
//    }
//}